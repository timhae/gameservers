{ config, lib, pkgs, ... }:
# TODO: move into gameservers
with lib;

let
  cfg = config.setup.service.satisfactory;
  varLibStateDir = "/var/lib/satisfactory";
in
{
  options = {
    setup.service.satisfactory.enable =
      mkEnableOption "Satisfactory dedicated server";
  };

  config = mkIf cfg.enable {

    networking.firewall.allowedUDPPorts = [ 15777 15000 7777 ];

    systemd.services.satisfactory-server = {
      description = "Satisfactory dedicated server";
      wantedBy = [ "multi-user.target" ];
      after = [ "network-online.target" ];
      environment = {
        # see ld library path in https://satisfactory.fandom.com/wiki/Dedicated_servers/Running_as_a_Service
        # LD_LIBRARY_PATH =
        #   "${varLibStateDir}/linux64:${varLibStateDir}/Engine/Binaries/Linux:${varLibStateDir}/Engine/Binaries/ThirdParty/PhysX3/Linux/x86_64-unknown-linux-gnu/";
        HOME = varLibStateDir;
      };
      serviceConfig = {
        ExecStartPre = [
          ''
            ${pkgs.steamcmd}/bin/steamcmd \
              +force_install_dir ${varLibStateDir} \
              +login anonymous \
              +app_update 1690800 \
              +quit
          ''
          "${pkgs.patchelf}/bin/patchelf --set-interpreter ${pkgs.glibc}/lib/ld-linux-x86-64.so.2 ${varLibStateDir}/Engine/Binaries/Linux/UE4Server-Linux-Shipping"
        ];
        ExecStart = ''
          ${varLibStateDir}/FactoryServer.sh -NOSTEAM
        '';
        Restart = "no";
        StateDirectory = "satisfactory";
        WorkingDirectory = varLibStateDir;
        DynamicUser = true;
      };
    };
  };
}
